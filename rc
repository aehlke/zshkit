# By default, all created elements are rwx______
umask 077

# Accept message from other users
mesg y

# Watch for login/logout
watch=all

function precmd
{
	# Color for decorations
	local deco="%{${fg_bold[black]}%}"

  	# Change path color given user rights on it
  	if [[ -O "${PWD}" ]]; 
	# owner
	then 
    	local path_color="${fg_no_bold[default]}"
  	elif [[ -w "${PWD}" ]]; 
	# can write
	then
    	local path_color="${fg_no_bold[blue]}"
  	# other
	else 
    	local path_color="${fg_no_bold[red]}"
  	fi

	# Change hostname color on knowm hosts
  	case ${HOST%%.*} in
    	my-Desktop) local host_color="${fg_bold[cyan]}" ;;
    	my-Laptop)  local host_color="${fg_bold[cyan]}" ;;
    	*)          local host_color="${fg_bold[red]}" ;;
  	esac

  	# Display return code when not 0
  	local return_code="%(?..${deco}!%{${fg_no_bold[red]}%}%?${deco}! )"
  	# user@host
  	local user_at_host="%{${fg_bold[red]}%}%n${deco}@%{${host_color}%}%m"
  	# Red # for root, plain % for user
  	local sign="%(!.%{${fg_bold[red]}%}.${deco})%#"

  	PS1="%T%B${deco}[${path_color}%~${deco}]%b
${return_code}${deco}[${user_at_host}${deco}] `git-prompt` ${sign}%{${reset_color}%} "
}
